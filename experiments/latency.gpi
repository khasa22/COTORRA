# handovers @{48, 100, 315, 363}
# Black regions to illustrate migration
# First region
set object 4 polygon from 0,0 to 0,190 to 45,190 to 45,0 to 0,0;
set object 4 fc "#d2e9f3" fillstyle transparent solid 1 noborder; # rgb "black" lw 1;
# Second region
set object 1 polygon from 51,0 to 51,190 to 97,190 to 97,0 to 51,0;
set object 1 fc "#eaebed" fillstyle transparent solid 1 noborder; #rgb "black" lw 1;
# Third region
set object 3 polygon from 103,0 to 103,190 to 312,190 to 312,0;
set object 3 fc "#ffeec4" fillstyle transparent solid 1 noborder; # rgb "black" lw 1;
# Fourth region
set object 2 polygon from 318,0 to 318,190 to 360,190 to 360,0;
set object 2 fc "#eaebed" fillstyle transparent solid 1 noborder; # rgb "black" lw 1;
# Fifth region
set object 5 polygon from 366,0 to 366,190 to 450,190 to 450,0;
set object 5 fc "#d2e9f3" fillstyle transparent solid 1 noborder; # rgb "black" lw 1;



# Franceso style grid
## set style line 13 lc rgb 'gray' lt 1 lw 1;
## #set grid ls 13;
## set grid y ls 13;
## set grid x ls 13;
set grid front ;
set xlabel "Time [sec]";
set ylabel "Service time [ms]";
set key outside tmargin maxcols 4 maxrows 2 center; 
set yrange [0:190];
set tics scale 0;
set term eps;
set border lw 3 lc "gray";
set output "/tmp/o.eps";
set tics textcolor rgb "black";


plot "soa2_p.txt" u 1:2 w lines t "SoA" lc "#e5ae38" lw 6,\
    "okpi_p.txt" u 1:2 w lines t "OKpi_e" lc "#008fd5" lw 8,\
    "< awk '(NR%15==0) {print $1,$2;}' 'theoretical.txt'"\
        u 1:2 w linespoints t "OKpi_t" lc "#275b00" lt 6 ps 0.7 lw 2,\
    15 w lines t "e2e" lc "#fc4f30" lw 5 dt 2,\
    NaN title "AP \\{1,3\\}" with boxes lt 2 fc rgb "#d2e9f3" fillstyle transparent solid 1 border,\
    NaN title "AP \\{2,4\\}" with boxes lt 2 fc rgb "#eaebed" fillstyle transparent solid 1 border,\
    NaN title "AP \\{5,6\\}" with boxes lt 2 fc rgb "#ffeec4" fillstyle transparent solid 1 border;
    #15 w linespoints  t "e2e" lc "#fc4f30" lt 7 ps 0.3;
    #15 w linespoints  t "e2e" lc "#fc4f30" lt 7 ps 0.3;




